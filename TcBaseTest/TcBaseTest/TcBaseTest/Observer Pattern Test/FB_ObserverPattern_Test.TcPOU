<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_ObserverPattern_Test" Id="{9669889e-5dc1-4a5e-b021-9809da0da496}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ObserverPattern_Test EXTENDS TcUnit.FB_TestSuite
VAR
    
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[TestAdd5CompatibleObserversExpectAll5();
TestAdd6IncompatibleObserversExpectNone();
TestAddMoreObserversThanAllowedExpectOnlyMaxCount();
TestCompatibleDataTypesExpectValuesToUpdate();
TestIncompatileDataTypesExpectNoValueChange();
]]></ST>
    </Implementation>
    <Method Name="TestAdd5CompatibleObserversExpectAll5" Id="{791da87e-3b1e-4df9-be32-6b1c00308983}">
      <Declaration><![CDATA[METHOD PRIVATE TestAdd5CompatibleObserversExpectAll5
VAR_INST
    fbObservableReal: FB_Observable_REAL(TRUE);

    fbObserverReal: ARRAY [1..5] OF FB_Observer_REAL;

    i: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('TestAdd5CompatibleObserversExpectAll5');

FOR i := 1 TO SIZEOF(fbObserverReal)/SIZEOF(fbObserverReal[1]) DO
    fbObservableReal.Attach(fbObserverReal[i]);
END_FOR

THIS^.AssertEquals_UDINT(Expected := 5,
                         Actual := fbObservableReal.nObserverCount,
                         Message := 'Unexpected number of observers attached');

TcUnit.TEST_FINISHED();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="TestAdd6IncompatibleObserversExpectNone" Id="{f008ceac-1587-4d6c-8c53-a475f778f5b0}">
      <Declaration><![CDATA[METHOD PRIVATE TestAdd6IncompatibleObserversExpectNone
VAR_INST
    // Test can be less manual with dynamic list
    fbObservableDint: FB_Observable_DINT(TRUE);
    fbObservableUdint: FB_Observable_UDINT(TRUE);
    fbObservableUint: FB_Observable_UINT(TRUE);
    fbObservableReal: FB_Observable_REAL(TRUE);
    fbObservableLreal: FB_Observable_LREAL(TRUE);

    fbObserverDint: ARRAY [1..6] OF FB_Observer_DINT;
    fbObserverUdint: ARRAY [1..6] OF FB_Observer_UDINT;
    fbObserverUint: ARRAY [1..6] OF FB_Observer_UINT;
    fbObserverLreal: ARRAY [1..6] OF FB_Observer_LREAL;
    fbObserverReal: ARRAY [1..6] OF FB_Observer_REAL;

    i: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('TestAdd6IncompatibleObserversExpectNone');

FOR i := 1 TO SIZEOF(fbObserverUdint)/SIZEOF(fbObserverUdint[1]) DO
    fbObservableDint.Attach(fbObserverUdint[i]);
    fbObservableDint.Attach(fbObserverUint[i]);
    fbObservableDint.Attach(fbObserverLreal[i]);
    fbObservableDint.Attach(fbObserverReal[i]);

    fbObservableUdint.Attach(fbObserverDint[i]);
    fbObservableUdint.Attach(fbObserverUint[i]);
    fbObservableUdint.Attach(fbObserverLreal[i]);
    fbObservableUdint.Attach(fbObserverReal[i]);

    fbObservableUint.Attach(fbObserverDint[i]);
    fbObservableUint.Attach(fbObserverUdint[i]);
    fbObservableUint.Attach(fbObserverLreal[i]);
    fbObservableUint.Attach(fbObserverReal[i]);

    fbObservableLreal.Attach(fbObserverDint[i]);
    fbObservableLreal.Attach(fbObserverUdint[i]);
    fbObservableLreal.Attach(fbObserverUint[i]);
    fbObservableLreal.Attach(fbObserverReal[i]);

    fbObservableReal.Attach(fbObserverDint[i]);
    fbObservableReal.Attach(fbObserverUdint[i]);
    fbObservableReal.Attach(fbObserverUint[i]);
    fbObservableReal.Attach(fbObserverLreal[i]);

END_FOR

THIS^.AssertEquals_UDINT(Expected := 0,
                         Actual := fbObservableDint.nObserverCount,
                         Message := 'Unexpected number of observers attached to observable type DINT');

THIS^.AssertEquals_UDINT(Expected := 0,
                         Actual := fbObservableUdint.nObserverCount,
                         Message := 'Unexpected number of observers attached to observable type UDINT');

THIS^.AssertEquals_UDINT(Expected := 0,
                         Actual := fbObservableUint.nObserverCount,
                         Message := 'Unexpected number of observers attached to observable type UINT');

THIS^.AssertEquals_UDINT(Expected := 0,
                         Actual := fbObservableLreal.nObserverCount,
                         Message := 'Unexpected number of observers attached to observable type LREAL');

THIS^.AssertEquals_UDINT(Expected := 0,
                         Actual := fbObservableReal.nObserverCount,
                         Message := 'Unexpected number of observers attached to observable type REAL');

TcUnit.TEST_FINISHED();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="TestAddMoreObserversThanAllowedExpectOnlyMaxCount" Id="{0c149026-c1e8-4c39-92b6-923e4107cb0e}">
      <Declaration><![CDATA[METHOD PRIVATE TestAddMoreObserversThanAllowedExpectOnlyMaxCount
VAR_INST
    fbObservableReal: FB_Observable_REAL(TRUE);

    fbObserverReal: ARRAY [1..(TcBase.Param_Observer.cMaxNumberOfObservers+10)] OF FB_Observer_REAL;

    i: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('TestAddMoreObserversThanAllowedExpectOnlyMaxCount');

FOR i := 1 TO SIZEOF(fbObserverReal)/SIZEOF(fbObserverReal[1]) DO
    fbObservableReal.Attach(fbObserverReal[i]);
END_FOR

THIS^.AssertEquals_UDINT(Expected := TcBase.Param_Observer.cMaxNumberOfObservers,
                         Actual := fbObservableReal.nObserverCount,
                         Message := 'Unexpected number of observers attached');

TcUnit.TEST_FINISHED();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="TestCompatibleDataTypesExpectValuesToUpdate" Id="{a0c42179-d282-4d90-9f50-7c032bf816e4}">
      <Declaration><![CDATA[METHOD PRIVATE TestCompatibleDataTypesExpectValuesToUpdate
VAR_INST
    fbObservableDint: FB_Observable_DINT(TRUE);
    fbObservableUdint: FB_Observable_UDINT(TRUE);
    fbObservableUint: FB_Observable_UINT(TRUE);
    fbObservableLreal: FB_Observable_LREAL(TRUE);
    fbObservableReal: FB_Observable_REAL(TRUE);

    fbObserverDint: FB_Observer_DINT;
    fbObserverUdint: FB_Observer_UDINT;
    fbObserverUint: FB_Observer_UINT;
    fbObserverLreal: FB_Observer_LREAL;
    fbObserverReal: FB_Observer_REAL;

    i: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('TestCompatibleDataTypesExpectValuesToUpdate');

fbObservableDint.Attach(fbObserverDint);
fbObservableUdint.Attach(fbObserverUdint);
fbObservableUint.Attach(fbObserverUint);
fbObservableLreal.Attach(fbObserverLreal);
fbObservableReal.Attach(fbObserverReal);

FOR i := 1 TO 10 DO
    fbObservableDint.nValue32 := TO_DINT(i);
    fbObservableUdint.nValue32 := i;
    fbObservableUint.nValue16 := TO_UINT(i);
    fbObservableLreal.fValue64 := TO_LREAL(i);
    fbObservableReal.fValue32 := TO_REAL(i);

    THIS^.AssertEquals_DINT(Expected := TO_DINT(i),
                            Actual := fbObserverDint.nValue32,
                            Message := 'DINT observer failed to update value according to observable');
    THIS^.AssertEquals_UDINT(Expected := i,
                             Actual := fbObserverUdint.nValue32,
                             Message := 'UDINT observer failed to update value according to observable');
    THIS^.AssertEquals_UINT(Expected := TO_UINT(i),
                            Actual := fbObserverUint.nValue16,
                            Message := 'UINT observer failed to update value according to observable');
    THIS^.AssertEquals_LREAL(Expected := TO_LREAL(i),
                             Actual := fbObserverLreal.fValue64,
                             Delta := 0.01,
                             Message := 'LREAL observer failed to update value according to observable');
    THIS^.AssertEquals_REAL(Expected := TO_REAL(i),
                            Actual := fbObserverReal.fValue32,
                            Delta := 0.01,
                            Message := 'REAL observer failed to update value according to observable');
END_FOR

TcUnit.TEST_FINISHED();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="TestIncompatileDataTypesExpectNoValueChange" Id="{0979a056-7376-4db3-99e3-658aa0013606}">
      <Declaration><![CDATA[METHOD PRIVATE TestIncompatileDataTypesExpectNoValueChange
VAR_INST
    fbObservableReal: FB_Observable_REAL(TRUE);

    fbObserverDint: FB_Observer_DINT;
    fbObserverUdint: FB_Observer_UDINT;
    fbObserverUint: FB_Observer_UINT;
    fbObserverLreal: FB_Observer_LREAL;

    i: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('TestIncompatileDataTypeObserverAndObservableExpectNoValueChange');

fbObservableReal.Attach(fbObserverDint);
fbObservableReal.Attach(fbObserverUdint);
fbObservableReal.Attach(fbObserverUint);
fbObservableReal.Attach(fbObserverLreal);

FOR i := 1 TO 10 DO
    fbObservableReal.fValue32 := TO_REAL(i);

    THIS^.AssertEquals_DINT(Expected := 0,
                            Actual := fbObserverDint.nValue32,
                            Message := 'DINT observer value is not 0, but it should be 0');

    THIS^.AssertEquals_UDINT(Expected := 0,
                            Actual := fbObserverUdint.nValue32,
                            Message := 'UDINT observer value is not 0, but it should be 0');

    THIS^.AssertEquals_UINT(Expected := 0,
                            Actual := fbObserverUint.nValue16,
                            Message := 'UINT observer value is not 0, but it should be 0');

    THIS^.AssertEquals_LREAL(Expected := 0,
                             Actual := fbObserverLreal.fValue64,
                             Delta := 0.001,
                             Message := 'LREAL observer value is not 0, but it should be 0');
END_FOR

TcUnit.TEST_FINISHED();
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>