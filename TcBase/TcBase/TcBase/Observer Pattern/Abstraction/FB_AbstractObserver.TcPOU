<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_AbstractObserver" Id="{59366400-b2f5-4598-9c5c-2530302a1d7f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ABSTRACT FB_AbstractObserver EXTENDS FB_Object IMPLEMENTS I_Observer
VAR
    _nTypeClass:        __SYSTEM.TYPE_CLASS;
    _pVal:              POINTER TO BYTE;
    _nSize:             DINT;
    _nUpdateCounter:    UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="_Update" Id="{04601ce4-ef10-086c-2ba8-698f7bbc7727}">
      <Declaration><![CDATA[METHOD PROTECTED _Update
VAR_INPUT
    pData:          POINTER TO BYTE;
    nTypeClass:     __SYSTEM.TYPE_CLASS;
    nSize:          UDINT;
END_VAR
VAR
    nMemcpyRes:     UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nTypeClass <> THIS^._nTypeClass THEN
    RETURN;
END_IF

IF nSize <> TO_UDINT(THIS^._nSize) THEN
    RETURN;
END_IF

IF pData = 0 THEN
    RETURN;
END_IF

nMemcpyRes := Tc2_System.MEMCPY(destAddr := THIS^._pVal,
                                srcAddr := pData,
                                n := TO_UDINT(THIS^._nSize));

IF nMemcpyRes <> 0 THEN
    THIS^._nUpdateCounter := THIS^._nUpdateCounter + 1;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="nTypeClass" Id="{fde940e3-79f9-098e-0135-d3adf4a5926d}">
      <Declaration><![CDATA[PROPERTY nTypeClass : __SYSTEM.TYPE_CLASS
]]></Declaration>
      <Get Name="Get" Id="{8c43525e-7ffb-0f69-01a8-b6b495032969}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nTypeClass := THIS^._nTypeClass;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="PostUpdate" Id="{30236f96-dbe0-0ab4-0064-2829402f1028}">
      <Declaration><![CDATA[METHOD PROTECTED PostUpdate
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="PreUpdate" Id="{dc87cb06-4c62-0279-1d2a-b00e97977a5f}">
      <Declaration><![CDATA[METHOD PROTECTED PreUpdate
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="Update" Id="{db1aebf2-fca2-494e-a52a-f9c476d5cd33}">
      <Declaration><![CDATA[{attribute 'hasanytype'}
METHOD FINAL Update
(* Template method for updating using ANY-typed variable *)
VAR_INPUT
    anyVal:         ANY;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.PreUpdate();

THIS^._Update(pData := anyVal.pValue,
              nTypeClass := anyVal.TypeClass,
              nSize := TO_UDINT(anyVal.diSize));

THIS^.PostUpdate();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="UpdateP" Id="{754e5201-247b-4bda-8c15-e31312212f2e}">
      <Declaration><![CDATA[METHOD FINAL UpdateP
(* Template method for updating using explicit
   variable information *)
VAR_INPUT
    pData:          POINTER TO BYTE;
    nTypeClass:     __SYSTEM.TYPE_CLASS;
    nSize:          UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.PreUpdate();

THIS^._Update(pData := pData,
              nTypeClass := nTypeClass,
              nSize := nSize);

THIS^.PostUpdate();
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>